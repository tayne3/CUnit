string(TOLOWER "${CMAKE_SYSTEM_NAME}" SYSTEM_NAME)
set(CUNIT_OUTPUT_DIR_DEBUG ${PROJECT_SOURCE_DIR}/bin/debug-${SYSTEM_NAME})
set(CUNIT_OUTPUT_DIR_RELEASE ${PROJECT_SOURCE_DIR}/bin/release-${SYSTEM_NAME})

if(NOT CMAKE_BUILD_TYPE)
	set(CMAKE_BUILD_TYPE Debug)
endif()
if(("${CMAKE_BUILD_TYPE}" STREQUAL "Debug") # For debugging purposes, includes symbol table and unoptimized code (-O0 -g)
	OR ("${CMAKE_BUILD_TYPE}" STREQUAL "RelWithDebInfo")) # Includes optimized code and symbol table for debugging when errors occur (-O2 -g -DNDEBUG)
	set(CUNIT_OUTPUT_DIR ${CUNIT_OUTPUT_DIR_DEBUG})
	set(CUNIT_BUILD_DEBUG ON)
elseif(("${CMAKE_BUILD_TYPE}" STREQUAL "Release") # For release purposes, includes optimized code and typically does not include symbol table (-O3 -DNDEBUG)
	OR ("${CMAKE_BUILD_TYPE}" STREQUAL "MinSizeRel")) # Minimizes binary size, enables optimizations, and removes unnecessary symbols (-Os -DNDEBUG)
	set(CUNIT_OUTPUT_DIR ${CUNIT_OUTPUT_DIR_RELEASE})
	set(CUNIT_BUILD_DEBUG OFF)
else()
	message(FATAL_ERROR "Unsupported build type: ${CMAKE_BUILD_TYPE}.")
endif()

if(NOT CMAKE_RUNTIME_OUTPUT_DIRECTORY)
	set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CUNIT_OUTPUT_DIR})					
endif()
if(NOT CMAKE_LIBRARY_OUTPUT_DIRECTORY)
	set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CUNIT_OUTPUT_DIR})					
endif()
if(NOT CMAKE_ARCHIVE_OUTPUT_DIRECTORY)
	set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CUNIT_OUTPUT_DIR})					
endif()

set(CMAKE_C_STANDARD 99)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS ON)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

include(GNUInstallDirs)
set(CMAKE_SKIP_BUILD_RPATH OFF)
set(CMAKE_BUILD_WITH_INSTALL_RPATH ON)
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH OFF)
if(APPLE)
    set(CMAKE_MACOSX_RPATH ON)
    set(CMAKE_INSTALL_RPATH "@loader_path;@loader_path/${CMAKE_INSTALL_LIBDIR}")
else()
    set(CMAKE_INSTALL_RPATH "$ORIGIN;$ORIGIN/${CMAKE_INSTALL_LIBDIR}")
endif()
